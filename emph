#!/usr/bin/perl
# emphasise lines matching regexes
# jes 2012

use strict;
use warnings;

use Term::ANSIColor;

@ARGV or die "Usage: emph PATTERN...\n";

while (<STDIN>) {
    chomp;

    my @colours = qw(red green yellow blue magenta cyan bright_red bright_green
        bright_yellow bright_blue bright_magenta bright_cyan);
    my $colouridx = 0;

    my $colour;

    PATTERN:
    foreach my $pattern (@ARGV) {
        if (/$pattern/) {
            $colour = $colours[$colouridx % @colours];
            last PATTERN;
        }
        $colouridx++;
    }

    print color $colour if $colour;
    print $_;
    print color 'reset' if $colour;
    print "\n";
}

__END__

=head1 NAME

emph - emphasise matching lines with colour

=head1 SYNOPSIS

 emph PATTERN...

=head1 DESCRIPTION

This program operates similarly to grep(1).  It takes input from stdin,
and colours any lines that match a regex described by one of the arguments.
Multiple patterns may be specified, and each one will receive a different
colour (well, there are 12 colours; they repeat after that).

Patterns specified sooner will match sooner and prevent matching of later
patterns.  For example:

  $ emph foo bar < file

Lines containing C<foo> will be red, and lines containing C<bar> but NOT
C<foo> will be green, and other lines will not be coloured.

=head1 AUTHOR

James Stanley <james@incoherency.co.uk>
